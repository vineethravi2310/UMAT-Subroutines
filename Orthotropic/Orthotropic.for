! WRITTEN BY VINEETH RAVI
! FOLLOWING IS A UMAT CODE FOR ORTHOTROPIC LINEAR ELASTIC MATERIAL
! VARIABLES FROM THE LETTER I TO N ARE CONSIDEREDED TO BE INTEGERS
! AND VARIABLES STARTING WITH OTHER LETTERS ARE DOUBLE PRECISION
! SO PLEASE BE CAREFUL IN NAMING THE VARIABLES
! EXAMPLES OF FEW DEFAULT INTEGERS : NTENS, NDI, NSHR
! EXAMPLES OF FEW DEFAULT REAL NUMBERS : DDSDDE, STRESS, STRAN
      
      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,JSTEP,KINC)

      INCLUDE 'ABA_PARAM.INC'

      CHARACTER*80 CMNAME
      DIMENSION STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),DDSDDT(NTENS),DRPLDE(NTENS),
     2 STRAN(NTENS),DSTRAN(NTENS),TIME(2),PREDEF(1),DPRED(1),
     3 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3),
     4 JSTEP(4)
      
      
      PARAMETER (ZERO = 0.0D0, ONE = 1.0D0, TWO = 2.0D0)
	 
      E11 = PROPS(1)    ! YOUNGS MODULUS IN 1 DIRECTION
      E22 = PROPS(2)    ! YOUNGS MODULUS IN 2 DIRECTION
      E33 = PROPS(3)    ! YOUNGS MODULUS IN 3 DIRECTION
      PR12 = PROPS(4)   ! MAJOR POISSONS RATIO IN 1-2 PLANE
      PR13 = PROPS(5)   ! MAJOR POISSONS RATIO IN 1-3 PLANE
      PR23 = PROPS(6)   ! MAJOR POISSONS RATIO IN 2-3 PLANE
      G12 = PROPS(7)    ! SHEAR MODULUS IN 1-2 PLANE
      G13 = PROPS(8)    ! SHEAR MODULUS IN 1-3 PLANE
      G23 = PROPS(9)    ! SHEAR MODULUS IN 2-3 PLANE
C	  
      PR21 = (E22*PR12)/E11  ! CALCULAION OF MINOR POISSONS RATIO
      PR31 = (E33*PR13)/E11
      PR32 = (E33*PR23)/E22
      DELTA = (ONE)/(ONE-PR12*PR21-PR23*PR32-PR13*PR31-TWO*PR21*PR32*PR13)
C	  
      DO I=1,NTENS  ! INITIALIZATION OF JACOBIAN MATRIX
          DO J=1,NTENS
              DDSDDE(I,J) = ZERO
          ENDDO
      ENDDO
C        
      DDSDDE(1,1) = (E11*DELTA*(ONE-PR23*PR32))
      DDSDDE(1,2) = (E11*DELTA*(PR21+PR31*PR23))
      DDSDDE(1,3) = (E11*DELTA*(PR31+PR21*PR32))
      DDSDDE(2,1) = (E11*DELTA*(PR21+PR31*PR23))
      DDSDDE(2,2) = (E22*DELTA*(ONE-PR13*PR31))
      DDSDDE(2,3) = (E22*DELTA*(PR32+PR12*PR31))
      DDSDDE(3,1) = (E11*DELTA*(PR31+PR21*PR32))
      DDSDDE(3,2) = (E22*DELTA*(PR32+PR12*PR31))
      DDSDDE(3,3) = (E33*DELTA*(ONE-PR12*PR21))
      DDSDDE(4,4) = G12
      DDSDDE(5,5) = G13
      DDSDDE(6,6) = G23
C        
      DO I=1, NTENS
          DO J=1, NTENS
              STRESS(I) = STRESS(I) + DDSDDE(I, J)*DSTRAN(J) ! UPDATING STRESS TENSOR
          END DO
      END DO
      
      RETURN
      END